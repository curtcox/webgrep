#!/usr/bin/env groovy

import com.sun.net.httpserver.*
import java.util.regex.Pattern

int port = 8080
int backlog = 0
dir = new File(args[0])

String searchPage()           { new File('search.html').text }
String decode(String value)   { java.net.URLDecoder.decode(value, 'UTF-8') }
String query(String uri)      { uri.split(Pattern.quote('='))[1] }
String searchFor(String x)    { "./search_script $dir $x".execute().text }
String exists(String uri)     { file(uri).exists() }
String contentsOf(String uri) { '<pre>' + file(uri).text + '</pre>' }
File   file(String uri)       { new File(dir.toString() + uri) }

String responseBodyFor(String uri) {
  println uri
       if (uri == '/')        searchPage()
  else if (uri.contains('=')) searchFor(decode(query(uri)))
  else if (exists(uri))       contentsOf(uri)
  else "$uri not found"
}

HttpServer.create(new InetSocketAddress(port), backlog).with {
    println "Serving contents of ${dir} on ${port}, hit Ctrl+C to exit."
    createContext("/") { http ->
        http.responseHeaders.add("Content-type", "text/html")
        http.sendResponseHeaders(200, 0)
        String uri = http.requestURI
        http.responseBody.withWriter { out -> out << responseBodyFor(uri) }
    }
    start()
}
